plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

def apikeyPropertiesFile = rootProject.file("api_key.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 31

    defaultConfig {

        applicationId "com.neo.marvelcharacters"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY'])
        buildConfigField("String", "SECRET_KEY", apikeyProperties['SECRET_KEY'])

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //ktx core
    implementation 'androidx.core:core-ktx:1.7.0'

    //ktx lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"

    //ktx lifecycle view model
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"

    //ktx activity
    implementation "androidx.activity:activity-ktx:1.4.0"

    //layout
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    //dagger hilt
    def hilt_version = "2.41"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    //retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.3"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    //coil-kt
    implementation("io.coil-kt:coil:1.4.0")

    //paging
    implementation "androidx.paging:paging-runtime:3.1.1"

    //tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}